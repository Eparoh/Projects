import Topology.Nets.Basic

noncomputable section

open Set Filter Topology Classical Function DirectedSet

namespace Net

/- ### Filter associated to a net ### -/

/- Given any net s: D â†’ X we can associate to it a filter in X, namely the filter of tails of s.
   The important property about this filter is that it has the same limit and accumulation points as the net. -/

def FNet {X D : Type*} [DirectedSet D] (s : D â†’ X) : Filter X := {
  sets := {A : Set X | âˆƒ d : D, âˆ€ d' : D, (d â‰¤ d' â†’ s d' âˆˆ A)}
  univ_sets := by
    simp only [Set.mem_setOf_eq, mem_univ, implies_true, exists_const]
  sets_of_superset := by
    intro A B Ain AsubB
    rcases Ain with âŸ¨d, sinAâŸ©
    rw [Set.mem_setOf_eq]
    use d
    intro d' dled'
    exact AsubB (sinA d' dled')
  inter_sets := by
    intro A B Ain Bin
    rcases Ain with âŸ¨d, sdinâŸ©
    rcases Bin with âŸ¨e, seinâŸ©
    rcases directed' d e with âŸ¨f, dlef, elefâŸ©
    rw [Set.mem_setOf_eq]
    use f
    intro d' fled'
    constructor
    Â· exact sdin d' (le_trans dlef fled')
    Â· exact sein d' (le_trans elef fled')
}

/- Furthermore, the filter is Nebot -/
instance FNet.instNeBot {X D : Type*} [DirectedSet D] (s : D â†’ X) : Filter.NeBot (FNet s) where
  ne' := by
    intro emptyinF
    simp only [â† empty_mem_iff_bot, FNet, Filter.mem_mk, Set.mem_setOf_eq, mem_empty_iff_false] at emptyinF
    rcases emptyinF with âŸ¨d, eqâŸ©
    exact eq d (le_refl d)

/- As announced, given a net s and a point x in X, x is a limit of s iff it is a limit of its associated filter, that is
   FNet s â‰¤ ğ“ x. -/
theorem limit_net_iff_filter {X D : Type*} [TopologicalSpace X] [DirectedSet D] (s : D â†’ X) (x: X) :
  Limit s x â†” FNet s â‰¤ ğ“ x := by
    constructor
    Â· intro limitsx
      intro U Unhds
      rcases limitsx U Unhds with âŸ¨d, eqâŸ©
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
      use d
    Â· intro limitFx U Unhds
      have UinF := limitFx Unhds
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq] at UinF
      assumption

/- As announced, given a net s and a point x in X, x is a cluster point of s iff it is a cluster point of its associated filter. -/
theorem clpoint_net_iff_filter {X D : Type*} [TopologicalSpace X] [DirectedSet D] (s : D â†’ X) (x: X) :
  ClusterPoint s x â†” ClusterPt x (FNet s) := by
    constructor
    Â· intro clpointsx
      rw [clusterPt_iff]
      intro U Unhds V VinF
      rw [nonempty_def]
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq] at VinF
      rcases VinF with âŸ¨d, eqâŸ©
      rcases clpointsx d U Unhds with âŸ¨e, dlee, seinUâŸ©
      use s e
      constructor
      Â· assumption
      Â· exact eq e dlee
    Â· intro clpointFx
      intro d U Unhds
      rw [clusterPt_iff] at clpointFx
      have : s '' {e: D | d â‰¤ e} âˆˆ FNet s := by
        simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
        use d
        intro d' dled'
        rw [mem_image]
        use d'
        constructor
        Â· assumption
        Â· rfl
      have := clpointFx Unhds this
      rcases this with âŸ¨z, zinU, zinVâŸ©
      rcases zinV with âŸ¨f, dlef, sfeqzâŸ©
      rw [Set.mem_setOf_eq] at dlef
      use f
      constructor
      Â· assumption
      Â· rw [sfeqz]
        assumption

/- Furthermore, observe that if the net s: D â†’ X is contained in a set A, that is for any d in D we have that s d âˆˆ A, then this
   set A belongs to the associated filter. -/
lemma net_in_set_implies_set_in_filter {X: Type*} [TopologicalSpace X] (A: Set X):
  âˆ€ (D: Type u_1) (h: DirectedSet D) (s: D â†’ X), (âˆ€ (d: D), s d âˆˆ A) â†’ A âˆˆ FNet s := by
    intro D h s sinA
    /- Observe that s'' {d: D | (default' D) â‰¤ d} is in FNet s by definition, so as FNet s is a filter and the image of s is
       contained in A, we conclude that A âˆˆ FNet s. -/
    have : s '' {d: D | (default' D) â‰¤ d} âˆˆ FNet s := by
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
      use default' D
      intro d' defled'
      use d'
      constructor
      Â· rw [Set.mem_setOf_eq]
        assumption
      Â· rfl
    apply Filter.sets_of_superset (FNet s) this
    intro x xin
    rcases xin with âŸ¨d, _, sdeqxâŸ©
    rw [â† sdeqx]
    exact sinA d

/- ### Net associated to a filter ### -/

/- Given any filter F in a set X we can construct a directed set associated to it as
    D := {(x, E): X Ã— Set X | x âˆˆ E âˆˆ F},
   where the order relation is given by
    (x, E) â‰¤ (y, E') â†” E' âŠ† E
   We can then associate a net to F by defining s: D â†’ X such that s (x, E) = x.

   Again, the important property about this net is that it has the same limit and cluster points as F. -/

def DirectedSetF {X: Type*} (F: Filter X) := {P : X Ã— Set X | P.1 âˆˆ P.2 âˆ§ P.2 âˆˆ F}

def NetF {X: Type*} (F: Filter X): DirectedSetF F â†’ X := fun (P: DirectedSetF F) â†¦ P.1.1

/- If F is NeBot, then the DirectedsetF is indeed a directed set and so NetF is indeed a net. -/
instance DirectedSetF.isntDirectedSet {X: Type*} (F: Filter X) [Filter.NeBot F] : DirectedSet (DirectedSetF F) where
  le := fun P Q â†¦ Q.1.2 âŠ† P.1.2
  le_refl := by
    intro P x xin
    exact xin
  le_trans := by
    intro P Q R PleQ QleR
    apply subset_trans QleR PleQ
  default := by
    /- Observe that X is Inhabited because as univ âˆˆ F and F is NeBot, we have that univ â‰  âˆ…. -/
    have : Inhabited X := by
      have : @univ X â‰  âˆ… := by
        intro univempty
        have := empty_not_mem F
        rw [â† univempty] at this
        have := Filter.univ_sets F
        contradiction
      rw [â† nonempty_iff_ne_empty, nonempty_def] at this
      exact inhabited_of_exists this
    let x := @Inhabited.default X _
    have : (x, univ) âˆˆ DirectedSetF F := by
      simp only [DirectedSetF, Set.mem_setOf_eq]
      exact And.intro (mem_univ x) univ_mem
    exact âŸ¨(x, univ), thisâŸ©
  directed := by
    /- Given any (z, P), (y, Q) in DirectedSetF F, we have that P, Q âˆˆ F so P âˆ© Q âˆˆ F. Then, as F is NeBot we have that P âˆ© Q â‰  âˆ…,
       so there exists some x âˆˆ P âˆ© Q, and so (x, P âˆ© Q) is an element of DirectedSetF F and (z, P) â‰¤ (x, P âˆ© Q), (y, Q) â‰¤ (x, P âˆ© Q). -/
    intro P Q
    have : P.1.2 âˆ© Q.1.2 âˆˆ F := by
      exact inter_mem (P.2.2) (Q.2.2)
    have : âˆƒ (x: X), x âˆˆ P.1.2 âˆ© Q.1.2 := by
      exact Eventually.exists this
    rcases this with âŸ¨x, xininterâŸ©
    have : (x, P.1.2 âˆ© Q.1.2) âˆˆ DirectedSetF F := by
      simp only [DirectedSetF, Set.mem_setOf_eq]
      constructor
      Â· assumption
      Â· assumption
    use âŸ¨(x, P.1.2 âˆ© Q.1.2), thisâŸ©
    constructor
    Â· exact Set.inter_subset_left
    Â· exact Set.inter_subset_right

/- Definition of the order relation -/
@[simp]
theorem DirectedSetF_le_iff {X: Type*} (F: Filter X) [Filter.NeBot F] : âˆ€ (P Q : DirectedSetF F), P â‰¤ Q â†” Q.1.2 âŠ† P.1.2 := by
  intro P Q
  rfl

/- As stated, a point x in X will be a limit point of F iff it is a limit point of NetF F. -/
theorem limit_filter_iff_net {X: Type*} [TopologicalSpace X] (F: Filter X) [Filter.NeBot F] (x: X) :
  F â‰¤ ğ“ x â†” Limit (NetF F) x := by
    constructor
    Â· intro limitFx
      intro U Unhds
      have : (x, U) âˆˆ DirectedSetF F := by
        simp only [DirectedSetF, Set.mem_setOf_eq]
        constructor
        Â· exact mem_of_mem_nhds Unhds
        Â· exact limitFx Unhds
      use âŸ¨(x, U), thisâŸ©
      intro e dlee
      simp only [NetF]
      simp only [DirectedSetF_le_iff] at dlee
      exact dlee e.2.1
    Â· intro limitsx U Unhds
      rcases limitsx U Unhds with âŸ¨d, eqâŸ©
      apply Filter.sets_of_superset F d.2.2
      intro y yind2
      have : (y, d.1.2) âˆˆ DirectedSetF F := by
        simp only [DirectedSetF, Set.mem_setOf_eq]
        exact And.intro yind2 d.2.2
      have := eq âŸ¨(y, d.1.2), thisâŸ© (by simp only [DirectedSetF_le_iff]; exact subset_rfl)
      simp only [NetF] at this
      assumption

/- As stated, a point x in X will be a cluster point of F iff it is a cluster point of NetF F. -/
theorem clpoint_filter_iff_net {X: Type*} [TopologicalSpace X] (F: Filter X) [h: Filter.NeBot F] (x: X) :
  ClusterPt x F â†” ClusterPoint (NetF F) x := by
    constructor
    Â· intro clpointFx
      intro d U Unhds
      rw [clusterPt_iff] at clpointFx
      rcases clpointFx Unhds d.2.2 with âŸ¨y, yinU, yindâŸ©
      use âŸ¨(y, d.1.2), And.intro yind d.2.2âŸ©
      constructor
      Â· simp only [DirectedSetF_le_iff]
        exact subset_rfl
      Â· simp only [NetF]
        assumption
    Â· intro clpoinsx
      rw [clusterPt_iff]
      intro U Unhds V VinF
      rcases NeBot.nonempty_of_mem h VinF with âŸ¨v, vinVâŸ©
      rcases clpoinsx âŸ¨(v, V), And.intro vinV VinFâŸ© U Unhds with âŸ¨e, vVlee, seinUâŸ©
      use e.1.1
      constructor
      Â· exact seinU
      Â· exact vVlee e.2.1


/- Furthermore, if A âˆˆ F, we can define a new directed set
    D' := {(x, E): X Ã— Set X | x âˆˆ E âˆˆ F âˆ§ E âŠ† A},
   where the order relation is the same
    (x, E) â‰¤ (y, E') â†” E' âŠ† E
   Then, we have an inclusion from D' into D which allows as to define a subnet of NetF which will be contained in A.
   Furthermore, if x is a limit point of F, then it will be a limit point of this subnet, and if x is a cluster point of the
   subnet, then it is a cluster point of F. -/

def DirectedSetFA {X: Type*} (F: Filter X) (A: Set X) (_: A âˆˆ F) := {P : X Ã— Set X | P.1 âˆˆ P.2 âˆ§ P.2 âˆˆ F âˆ§ P.2 âŠ† A}

instance DirectedSetFA.isntDirectedSet {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F) :
  DirectedSet (DirectedSetFA F A h) where
    le := fun P Q â†¦ Q.1.2 âŠ† P.1.2
    le_refl := by
      intro P x xin
      exact xin
    le_trans := by
      intro P Q R PleQ QleR
      apply subset_trans QleR PleQ
    default := by
      /- As A âˆˆ F and F is NeBot, we have that A â‰  âˆ… and so taking any a âˆˆ A we have that (a, A) âˆˆ DirectedSetFA F A. -/
      have : A â‰  âˆ… := by
        intro Aempty
        have := empty_not_mem F
        rw [â† Aempty] at this
        contradiction
      rw [â† nonempty_iff_ne_empty, nonempty_def] at this
      let a := Classical.choose this
      exact âŸ¨(a, A), And.intro (Classical.choose_spec this) (And.intro h subset_rfl)âŸ©
    directed := by
      intro P Q
      have : P.1.2 âˆ© Q.1.2 âˆˆ F := by
        exact inter_mem (P.2.2.1) (Q.2.2.1)
      have : âˆƒ (x: X), x âˆˆ P.1.2 âˆ© Q.1.2 := by
        exact Eventually.exists this
      rcases this with âŸ¨x, xininterâŸ©
      have : (x, P.1.2 âˆ© Q.1.2) âˆˆ DirectedSetFA F A h := by
        simp only [DirectedSetFA, Set.mem_setOf_eq]
        constructor
        Â· assumption
        Â· constructor
          Â· assumption
          Â· intro x xininter
            apply P.2.2.2
            exact xininter.1
      use âŸ¨(x, P.1.2 âˆ© Q.1.2), thisâŸ©
      constructor
      Â· exact Set.inter_subset_left
      Â· exact Set.inter_subset_right

/- Definition of the order relation -/
@[simp]
theorem DirectedSetFA_le_iff {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F):
  âˆ€ (P Q : DirectedSetFA F A h), P â‰¤ Q â†” Q.1.2 âŠ† P.1.2 := by
    intro P Q
    rfl

/- Here we define the stated inclusion and the related subnet -/
def NetFi {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F): DirectedSetFA F A h â†’ DirectedSetF F :=
  fun P â†¦ âŸ¨P, And.intro P.2.1 P.2.2.1âŸ©

def NetFA {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F): DirectedSetFA F A h â†’ X :=
  fun P â†¦ (NetF F âˆ˜ NetFi F A h) P

/- As stated, NetFA is indeed a subnet of NetF -/
theorem NetFA_subnet {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) :
  Subnet (NetF F) (NetFA F A h) := by
    use NetFi F A h
    constructor
    Â· /- Given (x, P) in DirectedSetF we have that P âˆˆ F and, as A âˆˆ F, we obtain that A âˆ© P âˆˆ F. As F is NeBot, there exists
         some y âˆˆ A âˆ© P. It is then clear that (y, A âˆ© P) is in DirectedSetFA and observe that given any (z, Q) in DirectedSetF
         such that (y, A âˆ© P) â‰¤ (z, Q) we have that Q âŠ† A âˆ© P and so Q âŠ† P which implies that (x, P) â‰¤ (z, Q) = NetFi (z, Q). -/
      intro d
      have : âˆƒ (y: X), y âˆˆ A âˆ© d.1.2 := by
        rw [â† nonempty_def, nonempty_iff_ne_empty]
        intro empty
        have : A âˆ© d.1.2 âˆˆ F := by
          exact inter_mem h d.2.2
        rw [empty] at this
        have := empty_not_mem F
        contradiction
      rcases this with âŸ¨y, yininterâŸ©
      have : (y, A âˆ© d.1.2) âˆˆ DirectedSetFA F A h := by
        simp only [DirectedSetFA, Set.mem_setOf_eq]
        constructor
        Â· exact yininter
        Â· constructor
          Â· exact inter_mem h d.2.2
          Â· intro a aininter
            exact aininter.1
      use âŸ¨(y, A âˆ© d.1.2), thisâŸ©
      intro e lee
      simp only [DirectedSetF_le_iff]
      simp only [DirectedSetFA_le_iff] at lee
      apply subset_trans lee
      intro x xininter
      exact xininter.2
    Â· rfl

/- As stated, NetFA is contained in A. -/
theorem NetFA_subset {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) :
  âˆ€ (d: DirectedSetFA F A h), (NetFA F A h) d âˆˆ A := by
    /- Indeed, given any (x, P) in DirectedSetFA we have that NetFA (x, P) = NetF (NetFi (x, P)) = NetF (x, P) = x and, as
       (x, P) is in DirectedSetFA, we have that x âˆˆ P âŠ† A, so x âˆˆ A. -/
    intro d
    simp only [comp_apply, NetFA]
    exact d.2.2.2 d.2.1

/- As stated, if x is a limit point of F, then it is a limit point of NetFA. -/
theorem limit_filter_implies_net {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) (x: X) :
  F â‰¤ ğ“ x â†’ Limit (NetFA F A h) x := by
    /- If x is a limit point of F, then we know it is a limit point of NetF and as NetFA is a subnet the result is obvious. -/
    intro limitFx
    rw [limit_filter_iff_net] at limitFx
    exact subnet_same_limit (NetFA_subnet F A h) limitFx

/- As stated, if x is a cluster point of NetFa, then it is a cluster point of F. -/
theorem clupoint_NetF_inclusion_implies_clpoint {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) (x: X):
  ClusterPoint (NetFA F A h) x â†’ ClusterPt x F := by
    /- If x is a cluster point of NetFA, then it will be a clusert point of NetF, and so of F. -/
    intro clpointsx
    have := subnet_clusterpoint_implies_net (NetFA_subnet F A h) clpointsx
    rw [â† clpoint_filter_iff_net] at this
    assumption
