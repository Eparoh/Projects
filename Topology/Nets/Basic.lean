import  Topology.Nets.DirectedSets

noncomputable section

open Set Filter Topology Classical Function DirectedSet

set_option trace.Meta.Tactic.simp false
namespace Net

/- ### Definitions about nets ### -/

def Limit {X D: Type*} [TopologicalSpace X] [DirectedSet D] (s : D â†’ X) (x: X) : Prop :=
  âˆ€ U âˆˆ ğ“ x, âˆƒ (dâ‚€ : D), âˆ€ (d : D), dâ‚€ â‰¤ d â†’ s d âˆˆ U

def ClusterPoint {X D: Type*} [TopologicalSpace X] [DirectedSet D] (s: D â†’ X) (x : X): Prop :=
  âˆ€ (d : D), âˆ€ U âˆˆ ğ“ x, âˆƒ (e : D), (d â‰¤ e âˆ§ s e âˆˆ U)

def Subnet {X D E: Type*} [DirectedSet D] [DirectedSet E] (s: D â†’ X) (s': E â†’ X) : Prop :=
  âˆƒ (i: E â†’ D), (âˆ€ (d : D), âˆƒ (eâ‚€ : E), âˆ€ (e : E), (eâ‚€ â‰¤ e â†’  d â‰¤ (i e))) âˆ§ s' = s âˆ˜ i

/- ### Basic results ### -/

/- Subsequences are subnets -/
theorem subsequence_is_subnet {X: Type} [TopologicalSpace X] (s s' : â„• â†’ X) :
  (âˆƒ (i: â„• â†’ â„•), StrictMono i âˆ§ s' = s âˆ˜ i) â†’ Subnet s s' := by
  intro h
  dsimp [Subnet]
  rcases h with âŸ¨i, stricmono_i, s'eqscompiâŸ©
  use i
  constructor
  Â· intro d
    use d
    intro e dlee
    exact le_trans dlee (StrictMono.id_le stricmono_i e)
  Â· assumption

/- If a net converges to x, then every subnet converges to x -/
theorem subnet_same_limit {X D E: Type*} [TopologicalSpace X] [DirectedSet D] [DirectedSet E]
  {s : D â†’ X} {s' : E â†’ X} {x : X} : Subnet s s' â†’ Limit s x â†’ Limit s' x := by
    intro subnet slimitx
    dsimp [Limit] at *
    intro U U_nhds
    dsimp [Subnet] at subnet
    rcases subnet with âŸ¨i, crec, compâŸ©
    rw [comp]
    rcases slimitx U U_nhds with âŸ¨d, eq_dâŸ© -- We take d so that if d â‰¤ d' then s(d') âˆˆ U
    rcases crec d with âŸ¨e, eq_eâŸ© -- We take e so that if e â‰¤ e' then d â‰¤ i(e')
    /- Then, if e â‰¤ e', we have that d â‰¤ i(e') and so s(i(e')) âˆˆ U as wanted -/
    use e
    intro e' elee'
    have := eq_e e' elee'
    have := eq_d (i e') this
    exact this

theorem subnet_clusterpoint_implies_net_clusterpoint {X D E: Type*} [TopologicalSpace X] [DirectedSet D] [DirectedSet E]
  {s : D â†’ X} {s' : E â†’ X} {x : X} : Subnet s s' â†’ ClusterPoint s' x â†’ ClusterPoint s x := by
    intro subnet clpoints'x
    dsimp [ClusterPoint] at *
    intro d U Unhds
    dsimp [Subnet] at subnet
    rcases subnet with âŸ¨i, crec, compâŸ©
    rcases crec d with âŸ¨eâ‚€, eqâŸ©
    rcases clpoints'x eâ‚€ U Unhds with âŸ¨e, eâ‚€lee, s'einUâŸ©
    use i e
    constructor
    Â· exact eq e eâ‚€lee
    Â· rw [â† @comp_apply D X E s i e, â† comp]
      assumption

/- A point x is an accumulation point of a net s iff there exists a subnet that converges to x -/
theorem ClPoint_iff_exists_subnet {X D: Type*} [TopologicalSpace X] [h: DirectedSet D] (s: D â†’ X) (x : X) :
  ClusterPoint s x â†” âˆƒ (E: Type (max u_1 u_2)) (h': DirectedSet E) (s': E â†’ X), (Subnet s s' âˆ§ Limit s' x) := by
    constructor
    Â· intro t
      dsimp [ClusterPoint] at t
      have existence : âˆ€ V âˆˆ ğ“ x, âˆ€ (d: D), âˆƒ (e: D), (d â‰¤ e âˆ§ s e âˆˆ V) := by
        intro V V_nhds d
        exact t d V V_nhds
      /- Since given any neighbourhood V of x and any d of D there exists an element e of D such that
         d â‰¤ e and s(e) âˆˆ V, we'll define i(V, d) = e and the subnet s' = s âˆ˜ i (with ğ“ x
         ordered by âŠ‡), so s'(V,d) âˆˆ V -/
      let i : {V | V âˆˆ ğ“ x} Ã— D â†’ D := fun (âŸ¨V, _âŸ©, d)  â†¦  if h: âˆƒ (e: D), (d â‰¤ e âˆ§ s e âˆˆ V) then Classical.choose h else d
      let s' : {V | V âˆˆ ğ“ x} Ã— D â†’ X := s âˆ˜ i
      use ({V | V âˆˆ ğ“ x} Ã— D), (@instProduct {V | V âˆˆ ğ“ x} D (instNeighbourhoodLeft x) h), s'
      constructor
      Â· unfold Subnet
        use i
        constructor
        Â· intro d
          /- Using (X, d), we have that if (X, d) â‰¤ e = (eâ‚, eâ‚‚), then d â‰¤ eâ‚‚ and, As eâ‚‚ â‰¤ i(e), we
             obtain that d â‰¤ i(e) -/
          use (âŸ¨univ, univ_memâŸ© , d)
          intro e t'
          simp only [Prod.le_def] at t'
          dsimp only [i]
          rw [dif_pos (existence e.1 e.1.2 e.2)] -- choose = i(e)
          have := Classical.choose_spec (existence e.1 e.1.2 e.2)
          apply le_trans t'.2 this.1
        Â· rfl
      Â· dsimp [Limit]
        intro U U_nhds
        /- Given any d in D, we can use (U, d). In fact, if (U, d) â‰¤ e = (eâ‚, eâ‚‚), then eâ‚ âŠ† U
           and s(i(e)) âˆˆ eâ‚, so s(i(e)) âˆˆ U -/
        let d := DirectedSet.default' D
        use (âŸ¨U, U_nhdsâŸ© , d)
        intro e le_e
        simp only [Prod.le_def] at le_e
        dsimp [s', i]
        rw [dif_pos (existence e.1.1 e.1.2 e.2)] -- choose = i(e)
        have := (Classical.choose_spec (existence e.1.1 e.1.2 e.2)).right
        exact le_e.1 this
    Â· intro t
      rcases t with âŸ¨E, h', s', subnet_s', limit_s'âŸ©
      dsimp [ClusterPoint]
      intro d U U_nhds
      /- As s' is a subnet of s, there exists an i: s'.D â†’ s.D such that there exists an eâ‚€ with the
         property that if eâ‚€ â‰¤ e, then d â‰¤ i(e). Furthermore, as s' converges to x there exists an eâ‚
         in s'.D such that if eâ‚ â‰¤ e, then s'(e) = s(i(e)) âˆˆ U. So, it suffices to use an e in s.D with
         eâ‚€, eâ‚ â‰¤ e. -/
      dsimp [Subnet] at subnet_s'
      rcases subnet_s' with âŸ¨i, i_crec, s'eqscompiâŸ©
      rcases i_crec d with âŸ¨eâ‚€, eâ‚€_eqâŸ©
      dsimp [Limit] at limit_s'
      rcases limit_s' U U_nhds with âŸ¨eâ‚, eâ‚_eqâŸ©
      rcases DirectedSet.directed' eâ‚€ eâ‚ with âŸ¨e, eâ‚€lee, eâ‚leeâŸ©
      use i e
      constructor
      Â· exact eâ‚€_eq e eâ‚€lee
      Â· have : s (i e) = (s âˆ˜ i) e := by
          rfl
        rw [this, â† s'eqscompi]
        apply eâ‚_eq e eâ‚lee

/- ### Relation between nets and filters ### -/

def FNet {X D : Type*} [DirectedSet D] (s : D â†’ X) : Filter X := {
  sets := {A : Set X | âˆƒ d : D, âˆ€ d' : D, (d â‰¤ d' â†’ s d' âˆˆ A)}
  univ_sets := by
    simp only [Set.mem_setOf_eq, mem_univ, implies_true, exists_const]
  sets_of_superset := by
    intro A B Ain AsubB
    rcases Ain with âŸ¨d, sinAâŸ©
    rw [Set.mem_setOf_eq]
    use d
    intro d' dled'
    exact AsubB (sinA d' dled')
  inter_sets := by
    intro A B Ain Bin
    rcases Ain with âŸ¨d, sdinâŸ©
    rcases Bin with âŸ¨e, seinâŸ©
    rcases directed' d e with âŸ¨f, dlef, elefâŸ©
    rw [Set.mem_setOf_eq]
    use f
    intro d' fled'
    constructor
    Â· exact sdin d' (le_trans dlef fled')
    Â· exact sein d' (le_trans elef fled')
}

instance FNet.instNeBot {X D : Type*} [DirectedSet D] (s : D â†’ X) : Filter.NeBot (FNet s) where
  ne' := by
    intro emptyinF
    simp only [â† empty_mem_iff_bot, FNet, Filter.mem_mk, Set.mem_setOf_eq, mem_empty_iff_false] at emptyinF
    rcases emptyinF with âŸ¨d, eqâŸ©
    exact eq d (le_refl d)

theorem limit_net_iff_filter {X D : Type*} [TopologicalSpace X] [DirectedSet D] (s : D â†’ X) (x: X) : Limit s x â†” FNet s â‰¤ ğ“ x := by
  constructor
  Â· intro limitsx
    intro U Unhds
    rcases limitsx U Unhds with âŸ¨d, eqâŸ©
    simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
    use d
  Â· intro limitFx U Unhds
    have UinF := limitFx Unhds
    simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq] at UinF
    assumption

theorem ClPoint_net_iff_filter {X D : Type*} [TopologicalSpace X] [DirectedSet D] (s : D â†’ X) (x: X) : ClusterPoint s x â†” ClusterPt x (FNet s) := by
  constructor
  Â· intro clpointsx
    rw [clusterPt_iff]
    intro U Unhds V VinF
    rw [nonempty_def]
    simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq] at VinF
    rcases VinF with âŸ¨d, eqâŸ©
    rcases clpointsx d U Unhds with âŸ¨e, dlee, seinUâŸ©
    use s e
    constructor
    Â· assumption
    Â· exact eq e dlee
  Â· intro clpointFx
    intro d U Unhds
    rw [clusterPt_iff] at clpointFx
    have : s '' {e: D | d â‰¤ e} âˆˆ FNet s := by
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
      use d
      intro d' dled'
      rw [mem_image]
      use d'
      constructor
      Â· assumption
      Â· rfl
    have := clpointFx Unhds this
    rcases this with âŸ¨z, zinU, zinVâŸ©
    rcases zinV with âŸ¨f, dlef, sfeqzâŸ©
    rw [Set.mem_setOf_eq] at dlef
    use f
    constructor
    Â· assumption
    Â· rw [sfeqz]
      assumption

def DirectedSetF {X: Type*} (F: Filter X) := {P : X Ã— Set X | P.1 âˆˆ P.2 âˆ§ P.2 âˆˆ F}

def NetF {X: Type*} (F: Filter X): DirectedSetF F â†’ X := fun (P: DirectedSetF F) â†¦ P.1.1

instance DirectedSetF.isntDirectedSet {X: Type*} (F: Filter X) [Filter.NeBot F] : DirectedSet (DirectedSetF F) where
  le := fun P Q â†¦ Q.1.2 âŠ† P.1.2
  le_refl := by
    intro P x xin
    exact xin
  le_trans := by
    intro P Q R PleQ QleR
    apply subset_trans QleR PleQ
  default := by
    have : Inhabited X := by
      have : @univ X â‰  âˆ… := by
        intro univempty
        have := empty_not_mem F
        rw [â† univempty] at this
        have := Filter.univ_sets F
        contradiction
      rw [â† nonempty_iff_ne_empty, nonempty_def] at this
      exact inhabited_of_exists this
    let x := @Inhabited.default X _
    have : (x, univ) âˆˆ DirectedSetF F := by
      simp only [DirectedSetF, Set.mem_setOf_eq]
      exact And.intro (mem_univ x) univ_mem
    exact âŸ¨(x, univ), thisâŸ©
  directed := by
    intro P Q
    have : P.1.2 âˆ© Q.1.2 âˆˆ F := by
      exact inter_mem (P.2.2) (Q.2.2)
    have : âˆƒ (x: X), x âˆˆ P.1.2 âˆ© Q.1.2 := by
      exact Eventually.exists this
    rcases this with âŸ¨x, xininterâŸ©
    have : (x, P.1.2 âˆ© Q.1.2) âˆˆ DirectedSetF F := by
      simp only [DirectedSetF, Set.mem_setOf_eq]
      constructor
      Â· assumption
      Â· assumption
    use âŸ¨(x, P.1.2 âˆ© Q.1.2), thisâŸ©
    constructor
    Â· exact Set.inter_subset_left
    Â· exact Set.inter_subset_right

theorem DirectedSetF_le_iff {X: Type*} (F: Filter X) [Filter.NeBot F] : âˆ€ (P Q : DirectedSetF F), P â‰¤ Q â†” Q.1.2 âŠ† P.1.2 := by
  intro P Q
  rfl

theorem limit_filter_iff_net {X: Type*} [TopologicalSpace X] (F: Filter X) [Filter.NeBot F] (x: X) :
  F â‰¤ ğ“ x â†” Limit (NetF F) x := by
    constructor
    Â· intro limitFx
      intro U Unhds
      have : (x, U) âˆˆ DirectedSetF F := by
        simp only [DirectedSetF, Set.mem_setOf_eq]
        constructor
        Â· exact mem_of_mem_nhds Unhds
        Â· exact limitFx Unhds
      use âŸ¨(x, U), thisâŸ©
      intro e dlee
      simp only [NetF]
      simp only [DirectedSetF_le_iff] at dlee
      exact dlee e.2.1
    Â· intro limitsx U Unhds
      rcases limitsx U Unhds with âŸ¨d, eqâŸ©
      apply Filter.sets_of_superset F d.2.2
      intro y yind2
      have : (y, d.1.2) âˆˆ DirectedSetF F := by
        simp only [DirectedSetF, Set.mem_setOf_eq]
        exact And.intro yind2 d.2.2
      have := eq âŸ¨(y, d.1.2), thisâŸ© (by simp only [DirectedSetF_le_iff]; exact subset_rfl)
      simp only [NetF] at this
      assumption

theorem ClPoint_filter_iff_net {X: Type*} [TopologicalSpace X] (F: Filter X) [h: Filter.NeBot F] (x: X) :
  ClusterPt x F â†” ClusterPoint (NetF F) x := by
    constructor
    Â· intro clpointFx
      intro d U Unhds
      rw [clusterPt_iff] at clpointFx
      rcases clpointFx Unhds d.2.2 with âŸ¨y, yinU, yindâŸ©
      use âŸ¨(y, d.1.2), And.intro yind d.2.2âŸ©
      constructor
      Â· simp only [DirectedSetF_le_iff]
        exact subset_rfl
      Â· simp only [NetF]
        assumption
    Â· intro clpoinsx
      rw [clusterPt_iff]
      intro U Unhds V VinF
      rcases NeBot.nonempty_of_mem h VinF with âŸ¨v, vinVâŸ©
      rcases clpoinsx âŸ¨(v, V), And.intro vinV VinFâŸ© U Unhds with âŸ¨e, vVlee, seinUâŸ©
      use e.1.1
      constructor
      Â· exact seinU
      Â· exact vVlee e.2.1

lemma net_in_set_implies_set_in_filter {X: Type*} [TopologicalSpace X] (A: Set X):
  âˆ€ (D: Type u_1) (h: DirectedSet D) (s: D â†’ X), (âˆ€ (d: D), s d âˆˆ A) â†’ A âˆˆ FNet s := by
    intro D h s sinA
    have : s '' {d: D | (default' D) â‰¤ d} âˆˆ FNet s := by
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
      use default' D
      intro d' defled'
      use d'
      constructor
      Â· rw [Set.mem_setOf_eq]
        assumption
      Â· rfl
    apply Filter.sets_of_superset (FNet s) this
    intro x xin
    rcases xin with âŸ¨d, _, sdeqxâŸ©
    rw [â† sdeqx]
    exact sinA d

def DirectedSetFA {X: Type*} (F: Filter X) (A: Set X) (_: A âˆˆ F) := {P : X Ã— Set X | P.1 âˆˆ P.2 âˆ§ P.2 âˆˆ F âˆ§ P.2 âŠ† A}

instance DirectedSetFA.isntDirectedSet {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F) : DirectedSet (DirectedSetFA F A h) where
  le := fun P Q â†¦ Q.1.2 âŠ† P.1.2
  le_refl := by
    intro P x xin
    exact xin
  le_trans := by
    intro P Q R PleQ QleR
    apply subset_trans QleR PleQ
  default := by
    have : A â‰  âˆ… := by
      intro Aempty
      have := empty_not_mem F
      rw [â† Aempty] at this
      contradiction
    rw [â† nonempty_iff_ne_empty, nonempty_def] at this
    let a := Classical.choose this
    exact âŸ¨(a, A), And.intro (Classical.choose_spec this) (And.intro h subset_rfl)âŸ©
  directed := by
    intro P Q
    have : P.1.2 âˆ© Q.1.2 âˆˆ F := by
      exact inter_mem (P.2.2.1) (Q.2.2.1)
    have : âˆƒ (x: X), x âˆˆ P.1.2 âˆ© Q.1.2 := by
      exact Eventually.exists this
    rcases this with âŸ¨x, xininterâŸ©
    have : (x, P.1.2 âˆ© Q.1.2) âˆˆ DirectedSetFA F A h := by
      simp only [DirectedSetFA, Set.mem_setOf_eq]
      constructor
      Â· assumption
      Â· constructor
        Â· assumption
        Â· intro x xininter
          apply P.2.2.2
          exact xininter.1
    use âŸ¨(x, P.1.2 âˆ© Q.1.2), thisâŸ©
    constructor
    Â· exact Set.inter_subset_left
    Â· exact Set.inter_subset_right

def NFinclusion {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F)  : DirectedSetFA F A h â†’ DirectedSetF F := fun P â†¦ âŸ¨P, And.intro P.2.1 P.2.2.1âŸ©

theorem DirectedSetFA_le_iff {X: Type*} (F: Filter X) [Filter.NeBot F] (A: Set X) (h: A âˆˆ F) : âˆ€ (P Q : DirectedSetFA F A h), P â‰¤ Q â†” Q.1.2 âŠ† P.1.2 := by
  intro P Q
  rfl

theorem NetF_inclusion_in_set {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) :
  âˆ€ (d: DirectedSetFA F A h), (NetF F âˆ˜ NFinclusion F A h) d âˆˆ A := by
    intro d
    simp only [comp_apply, NetF]
    exact d.2.2.2 d.2.1

theorem NetF_inclusion_subnet {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) :
  Subnet (NetF F) (NetF F âˆ˜ NFinclusion F A h) := by
    use NFinclusion F A h
    constructor
    Â· intro d
      have : âˆƒ (y: X), y âˆˆ A âˆ© d.1.2 := by
        rw [â† nonempty_def, nonempty_iff_ne_empty]
        intro empty
        have : A âˆ© d.1.2 âˆˆ F := by
          exact inter_mem h d.2.2
        rw [empty] at this
        have := empty_not_mem F
        contradiction
      rcases this with âŸ¨y, yininterâŸ©
      have : (y, A âˆ© d.1.2) âˆˆ DirectedSetFA F A h := by
        simp only [DirectedSetFA, Set.mem_setOf_eq]
        constructor
        Â· exact yininter
        Â· constructor
          Â· exact inter_mem h d.2.2
          Â· intro a aininter
            exact aininter.1
      use âŸ¨(y, A âˆ© d.1.2), thisâŸ©
      intro e lee
      simp only [DirectedSetF_le_iff]
      simp only [DirectedSetFA_le_iff] at lee
      apply subset_trans lee
      intro x xininter
      exact xininter.2
    Â· rfl

theorem NetF_inclusion_same_limit {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) (x: X) :
  F â‰¤ ğ“ x â†’ Limit (NetF F âˆ˜ NFinclusion F A h) x := by
    intro limitFx
    rw [limit_filter_iff_net] at limitFx
    exact subnet_same_limit (NetF_inclusion_subnet F A h) limitFx

theorem clupoint_NetF_inclusion_implies_clpoint {X: Type*} [TopologicalSpace X] (F: Filter X) [NeBot F] (A: Set X) (h: A âˆˆ F) (x: X) :
  ClusterPoint (NetF F âˆ˜ NFinclusion F A h) x â†’ ClusterPt x F := by
    intro clpointsx
    have := subnet_clusterpoint_implies_net_clusterpoint (NetF_inclusion_subnet F A h) clpointsx
    rw [â† ClPoint_filter_iff_net] at this
    assumption

/- ### Classic characterizations ### -/

theorem mem_closure_iff_exists_filter {X: Type*} [TopologicalSpace X] (A: Set X) (x : X) :
  x âˆˆ closure A â†” âˆƒ (F: Filter X), F.NeBot âˆ§  A âˆˆ F âˆ§ F â‰¤ ğ“ x := by
    constructor
    Â· intro xinclos
      let G:= Filter.generate ({U | U âˆˆ ğ“ x} âˆª {A})
      use G
      constructor
      Â· rw [Filter.generate_neBot_iff]
        intro Y Ysub Yfin
        by_cases AinY : A âˆˆ Y
        Â· have : â‹‚â‚€ (Y \ {A}) âˆˆ ğ“ x := by
            rw [Filter.sInter_mem]
            Â· intro U UinY
              simp at UinY
              rcases Ysub UinY.1 with h | h
              Â· assumption
              Â· rw [mem_singleton_iff] at h
                have := UinY.2
                contradiction
            Â· exact Finite.diff Yfin {A}
          rw [mem_closure_iff_nhds] at xinclos
          have:= xinclos (â‹‚â‚€ (Y \ {A})) this
          have : â‹‚â‚€ (Y \ {A}) âˆ© A = â‹‚â‚€ Y := by
            ext y
            constructor
            Â· intro yin
              simp at *
              intro t tinY
              by_cases teqA : t = A
              Â· rw [teqA]
                exact yin.2
              Â· exact yin.1 t tinY teqA
            Â· intro yin
              simp at *
              constructor
              Â· intro t tinY _
                exact yin t tinY
              Â· exact yin A AinY
          rw [â† this]
          assumption
        Â· have : â‹‚â‚€ Y âˆˆ ğ“ x := by
            rw [Filter.sInter_mem]
            Â· intro U UinY
              rcases Ysub UinY with h | h
              Â· exact h
              Â· rw [mem_singleton_iff] at h
                rw [h] at UinY
                contradiction
            Â· assumption
          use x
          exact mem_of_mem_nhds this
      Â· constructor
        Â· apply mem_generate_of_mem
          simp
        Â· intro U Unhds
          apply mem_generate_of_mem
          simp
          right
          assumption
    Â· intro h
      rcases h with âŸ¨F, Fnebot, AinF, limitFxâŸ©
      rw [mem_closure_iff_nhds]
      intro U Unhds
      have : U âˆ© A âˆˆ F := by
        exact inter_mem (limitFx Unhds) AinF
      exact NeBot.nonempty_of_mem Fnebot this

theorem mem_closure_iff_exists_net {X: Type*} [TopologicalSpace X] (A: Set X) (x : X):
  x âˆˆ closure A â†” âˆƒ (D: Type u_1) (h: DirectedSet D) (s: D â†’ X), (âˆ€ (d: D), s d âˆˆ A) âˆ§ Limit s x := by
    have : Inhabited X := by
      exact { default := x }
    rw [mem_closure_iff_exists_filter]
    constructor
    Â· intro cond
      rcases cond with âŸ¨F, Fnebot, AinF, limitFxâŸ©
      use DirectedSetFA F A AinF, DirectedSetFA.isntDirectedSet F A AinF , NetF F âˆ˜ NFinclusion F A AinF
      exact And.intro (NetF_inclusion_in_set F A AinF) (NetF_inclusion_same_limit F A AinF x limitFx)
    Â· intro cond
      rcases cond with âŸ¨D, h, s, sinA, limitsxâŸ©
      use FNet s
      exact And.intro (FNet.instNeBot s) (And.intro (net_in_set_implies_set_in_filter A D h s sinA)
        ((limit_net_iff_filter s x).mp limitsx))

theorem isClosed_iff_limit_self {X: Type*} [TopologicalSpace X] (C: Set X) :
  IsClosed C â†” âˆ€ (x : X), âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d: D), s d âˆˆ C) â†’ Limit s x â†’ x âˆˆ C := by
    rw [isClosed_iff_forall_filter]
    constructor
    Â· intro cond
      intro x D h s sinC limitsx
      have : FNet s â‰¤ ğ“Ÿ C := by
        rw [le_principal_iff]
        exact net_in_set_implies_set_in_filter C D h s sinC
      exact cond x (FNet s) (FNet.instNeBot s) this ((limit_net_iff_filter s x).mpr limitsx)
    Â· intro cond
      intro x F Fnebot CinF limitFx
      rw [le_principal_iff] at CinF
      exact cond x (DirectedSetFA F C CinF) (DirectedSetFA.isntDirectedSet F C CinF) (NetF F âˆ˜ NFinclusion F C CinF)
        (NetF_inclusion_in_set F C CinF) (NetF_inclusion_same_limit F C CinF x limitFx)

theorem compact_iff_net_has_accumulationpoint {X : Type*} [TopologicalSpace X] (K: Set X) : IsCompact K â†”
  K = âˆ… âˆ¨ âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’ (âˆƒ x âˆˆ K, ClusterPoint s x) := by
    constructor
    Â· intro Kcomp
      by_cases Kem : K = âˆ…
      Â· left
        assumption
      Â· right
        intro D h s sinK
        simp only [IsCompact] at Kcomp
        rcases Kcomp ((le_principal_iff).mpr (net_in_set_implies_set_in_filter K D h s sinK)) with âŸ¨x, xinK, clpointFxâŸ©
        use x
        exact And.intro xinK ((ClPoint_net_iff_filter s x).mpr clpointFx)
    Â· intro cond
      rcases cond with cond | cond
      Â· rw [cond]
        exact isCompact_empty
      Â· simp only [IsCompact]
        intro F Fnebot KinF
        rw [le_principal_iff] at KinF
        rcases cond (DirectedSetFA F K KinF) (DirectedSetFA.isntDirectedSet F K KinF) (NetF F âˆ˜ NFinclusion F K KinF)
          (NetF_inclusion_in_set F K KinF) with âŸ¨x, xinK, clpointâŸ©
        use x
        exact And.intro xinK (clupoint_NetF_inclusion_implies_clpoint F K KinF x clpoint)

theorem compact_iff_net_has_convergent_subnet {X : Type*} [TopologicalSpace X] (K: Set X) : IsCompact K â†”
  K = âˆ… âˆ¨ âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’
  (âˆƒ (E: Type u_1) (h': DirectedSet E) (s': E â†’ X), âˆƒ x âˆˆ K, Subnet s s' âˆ§ Limit s' x) := by
    have : (âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’ âˆƒ (E: Type u_1) (h': DirectedSet E) (s': E â†’ X), âˆƒ x âˆˆ K, Subnet s s' âˆ§ Limit s' x) â†”
             (âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’ âˆƒ x âˆˆ K, ClusterPoint s x) := by
              constructor
              Â· intro t D h s dinK
                rcases t D h s dinK with âŸ¨E, h', s', x, xinK, eqâŸ©
                use x, xinK
                rw [ClPoint_iff_exists_subnet]
                use E, h', s'
              Â· intro t D h s dinK
                rcases t D h s dinK with âŸ¨x, xinK, eqâŸ©
                rw [ClPoint_iff_exists_subnet] at eq
                rcases eq with âŸ¨E, h', s', eqâŸ©
                use E, h', s', x
    rw [compact_iff_net_has_accumulationpoint, this]

theorem continuous_iff_image_of_net_converges {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] (f: X â†’ Y) (x : X):
  ContinuousAt f x â†” âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), Limit s x â†’ Limit (f âˆ˜ s) (f x) := by
    constructor
    Â· intro fcontatx D h s limitsx
      unfold ContinuousAt at fcontatx
      rw [Filter.tendsto_def] at fcontatx
      rw [limit_net_iff_filter]
      intro V Vnhds
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
      have := limitsx (f â»Â¹' V) (fcontatx V Vnhds)
      simp only [mem_preimage] at this
      simp only [comp_apply]
      assumption
    Â· intro cond
      unfold ContinuousAt
      rw [Filter.tendsto_def]
      intro V Vnhds
      have : Limit (NetF (ğ“ x)) x := by
        intro U Unhds
        use âŸ¨(x, U), And.intro (mem_of_mem_nhds Unhds) UnhdsâŸ©
        intro d xUled
        simp only [NetF]
        simp only [DirectedSetF_le_iff] at xUled
        exact xUled d.2.1
      rcases cond (DirectedSetF (ğ“ x)) (DirectedSetF.isntDirectedSet (ğ“ x)) (NetF (ğ“ x)) this V Vnhds with âŸ¨d, eqâŸ©
      have : d.1.2 âŠ† f â»Â¹' V := by
        intro z zind2
        rw [mem_preimage]
        have : d â‰¤ âŸ¨(z, d.1.2), And.intro zind2 d.2.2âŸ© := by
          rw [DirectedSetF_le_iff]
        have := eq âŸ¨(z, d.1.2), And.intro zind2 d.2.2âŸ© this
        simp only [NetF, comp_apply] at this
        assumption
      exact mem_of_superset d.2.2 this

theorem t2_iff_net_unique_limit_filter {X : Type*} [TopologicalSpace X] :
  T2Space X â†” âˆ€ (F: Filter X) (_: Filter.NeBot F) (x y : X), F â‰¤ ğ“ x â†’ F â‰¤ ğ“ y â†’ x = y := by
    constructor
    Â· intro t2
      intro F h x y limitFx limitFy
      by_contra! xneqy
      rw [â† disjoint_nhds_nhds] at xneqy
      unfold Disjoint at xneqy
      have := xneqy limitFx limitFy
      simp at this
      rw [Filter.neBot_iff] at h
      contradiction
    Â· intro cond
      rw [t2Space_iff_disjoint_nhds]
      by_contra not_haus
      dsimp only [Pairwise] at not_haus
      push_neg at not_haus
      rcases not_haus with âŸ¨x, y, xneqy, disjnhdsâŸ©
      dsimp only [Disjoint] at disjnhds
      push_neg at disjnhds
      rcases disjnhds with âŸ¨F, limitFx, limitFy, nebotFâŸ©
      simp at nebotF
      rw [â† Ne, â† Filter.neBot_iff] at nebotF
      exact xneqy (cond F nebotF x y limitFx limitFy)

theorem t2_iff_net_unique_limit {X : Type*} [TopologicalSpace X] :
  T2Space X â†” âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X) (x y : X), Limit s x â†’ Limit s y â†’ x = y := by
    rw [t2_iff_net_unique_limit_filter]
    constructor
    Â· intro cond
      intro D h s x y limitsx limitsy
      rw [limit_net_iff_filter] at *
      exact cond (FNet s) (FNet.instNeBot s) x y limitsx limitsy
    Â· intro cond F Fnebot x y limitFx limitFy
      rw [limit_filter_iff_net] at *
      exact cond (DirectedSetF F) (DirectedSetF.isntDirectedSet F) (NetF F) x y limitFx limitFy
