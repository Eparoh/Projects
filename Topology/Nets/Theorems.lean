import Topology.Nets.Filter
import Mathlib.Topology.UniformSpace.Cauchy

noncomputable section

open Set Filter Topology Classical Function DirectedSet

namespace Net

/- ### Classic characterizations ### -/

/- In this file we stated the characterizations of closure, closed set, compact set, continuous function at a point and Hausdorff set
   in terms of nets.

   To do so, we use the equivalent results for filters and the relations between nets and filter. -/

/- ### Missing results for filters ### -/

/- Here we stablish a couple of results about filters. Mainly a characterization for the closure and for a Hausdorff space. -/

/- An element x of X is in the closure of A iff there exists a filter F in X such that it is NeBot, A âˆˆ F and x is a limit
   point of F. -/
theorem mem_closure_iff_exists_filter {X: Type*} [TopologicalSpace X] (A: Set X) (x : X) :
  x âˆˆ closure A â†” âˆƒ (F: Filter X), F.NeBot âˆ§  A âˆˆ F âˆ§ F â‰¤ ğ“ x := by
    constructor
    Â· intro xinclos
      let G:= Filter.generate ({U | U âˆˆ ğ“ x} âˆª {A})
      use G
      constructor
      Â· rw [Filter.generate_neBot_iff]
        intro Y Ysub Yfin
        by_cases AinY : A âˆˆ Y
        Â· have : â‹‚â‚€ (Y \ {A}) âˆˆ ğ“ x := by
            rw [Filter.sInter_mem]
            Â· intro U UinY
              simp only [Set.mem_diff, Set.mem_singleton_iff] at UinY
              rcases Ysub UinY.1 with h | h
              Â· assumption
              Â· rw [mem_singleton_iff] at h
                have := UinY.2
                contradiction
            Â· exact Finite.diff Yfin {A}
          rw [mem_closure_iff_nhds] at xinclos
          have:= xinclos (â‹‚â‚€ (Y \ {A})) this
          have : â‹‚â‚€ (Y \ {A}) âˆ© A = â‹‚â‚€ Y := by
            ext y
            constructor
            Â· intro yin
              simp only [Set.union_singleton, Set.mem_inter_iff, Set.mem_sInter, Set.mem_diff, Set.mem_singleton_iff, and_imp]  at *
              intro t tinY
              by_cases teqA : t = A
              Â· rw [teqA]
                exact yin.2
              Â· exact yin.1 t tinY teqA
            Â· intro yin
              simp only [Set.union_singleton, Set.mem_inter_iff, Set.mem_sInter, Set.mem_diff, Set.mem_singleton_iff, and_imp]  at *
              constructor
              Â· intro t tinY _
                exact yin t tinY
              Â· exact yin A AinY
          rw [â† this]
          assumption
        Â· have : â‹‚â‚€ Y âˆˆ ğ“ x := by
            rw [Filter.sInter_mem]
            Â· intro U UinY
              rcases Ysub UinY with h | h
              Â· exact h
              Â· rw [mem_singleton_iff] at h
                rw [h] at UinY
                contradiction
            Â· assumption
          use x
          exact mem_of_mem_nhds this
      Â· constructor
        Â· apply mem_generate_of_mem
          simp only [Set.union_singleton, Set.mem_insert_iff, eq_self, Set.mem_setOf_eq, true_or]
        Â· intro U Unhds
          apply mem_generate_of_mem
          simp only [Set.union_singleton, Set.mem_insert_iff, Set.mem_setOf_eq]
          right
          assumption
    Â· intro h
      rcases h with âŸ¨F, Fnebot, AinF, limitFxâŸ©
      rw [mem_closure_iff_nhds]
      intro U Unhds
      have : U âˆ© A âˆˆ F := by
        exact inter_mem (limitFx Unhds) AinF
      exact NeBot.nonempty_of_mem Fnebot this

/- A topological space X is T2 iff every NeBot filter F in X has at most one limit point. -/
theorem t2_iff_net_unique_limit_filter {X : Type*} [TopologicalSpace X] :
  T2Space X â†” âˆ€ (F: Filter X) (_: Filter.NeBot F) (x y : X), F â‰¤ ğ“ x â†’ F â‰¤ ğ“ y â†’ x = y := by
    constructor
    Â· intro t2
      intro F h x y limitFx limitFy
      by_contra! xneqy
      rw [â† disjoint_nhds_nhds] at xneqy
      unfold Disjoint at xneqy
      have := xneqy limitFx limitFy
      simp only [le_bot_iff] at this
      rw [Filter.neBot_iff] at h
      contradiction
    Â· intro cond
      rw [t2Space_iff_disjoint_nhds]
      by_contra not_haus
      unfold Pairwise at not_haus
      push_neg at not_haus
      rcases not_haus with âŸ¨x, y, xneqy, disjnhdsâŸ©
      unfold Disjoint at disjnhds
      push_neg at disjnhds
      rcases disjnhds with âŸ¨F, limitFx, limitFy, nebotFâŸ©
      simp only [le_bot_iff] at nebotF
      rw [â† Ne, â† Filter.neBot_iff] at nebotF
      exact xneqy (cond F nebotF x y limitFx limitFy)

/- ### Characterizations in terms of nets ### -/

/- An element x of X is in the closure of A iff there exists a net s: D â†’ X such that it is contained in A and
   converges to x. -/
theorem mem_closure_iff_exists_net {X: Type*} [TopologicalSpace X] (A: Set X) (x : X):
  x âˆˆ closure A â†” âˆƒ (D: Type u_1) (h: DirectedSet D) (s: D â†’ X), (âˆ€ (d: D), s d âˆˆ A) âˆ§ Limit s x := by
    have : Inhabited X := by
      exact { default := x }
    rw [mem_closure_iff_exists_filter]
    constructor
    Â· intro cond
      rcases cond with âŸ¨F, Fnebot, AinF, limitFxâŸ©
      use DirectedSetFA F A AinF, DirectedSetFA.isntDirectedSet F A AinF , NetFA F A AinF
      exact And.intro (NetFA_subset F A AinF) (limit_filter_implies_net F A AinF x limitFx)
    Â· intro cond
      rcases cond with âŸ¨D, h, s, sinA, limitsxâŸ©
      use FNet s
      exact And.intro (FNet.instNeBot s) (And.intro (net_in_set_implies_set_in_filter A D h s sinA)
        ((limit_net_iff_filter s x).mp limitsx))

/- A set C of X is closed iff for every x in X and every net s: D â†’ X contained in C that converges to x we have that x âˆˆ C. -/
theorem isClosed_iff_limit_self {X: Type*} [TopologicalSpace X] (C: Set X) :
  IsClosed C â†” âˆ€ (x : X), âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d: D), s d âˆˆ C) â†’ Limit s x â†’ x âˆˆ C := by
    rw [isClosed_iff_forall_filter]
    constructor
    Â· intro cond
      intro x D h s sinC limitsx
      have : FNet s â‰¤ ğ“Ÿ C := by
        rw [le_principal_iff]
        exact net_in_set_implies_set_in_filter C D h s sinC
      exact cond x (FNet s) (FNet.instNeBot s) this ((limit_net_iff_filter s x).mpr limitsx)
    Â· intro cond
      intro x F Fnebot CinF limitFx
      rw [le_principal_iff] at CinF
      exact cond x (DirectedSetFA F C CinF) (DirectedSetFA.isntDirectedSet F C CinF) (NetFA F C CinF)
        (NetFA_subset F C CinF) (limit_filter_implies_net F C CinF x limitFx)

/- A set K of X is compact iff (K is empty or) any net s: D â†’ X contained in K has a cluster point x such that x âˆˆ K. -/
theorem compact_iff_net_has_accumulationpoint {X : Type*} [TopologicalSpace X] (K: Set X) : IsCompact K â†”
  K = âˆ… âˆ¨ âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’ (âˆƒ x âˆˆ K, ClusterPoint s x) := by
    constructor
    Â· intro Kcomp
      by_cases Kem : K = âˆ…
      Â· left
        assumption
      Â· right
        intro D h s sinK
        simp only [IsCompact] at Kcomp
        rcases Kcomp ((le_principal_iff).mpr (net_in_set_implies_set_in_filter K D h s sinK)) with âŸ¨x, xinK, clpointFxâŸ©
        use x
        exact And.intro xinK ((clpoint_net_iff_filter s x).mpr clpointFx)
    Â· intro cond
      rcases cond with cond | cond
      Â· rw [cond]
        exact isCompact_empty
      Â· simp only [IsCompact]
        intro F Fnebot KinF
        rw [le_principal_iff] at KinF
        rcases cond (DirectedSetFA F K KinF) (DirectedSetFA.isntDirectedSet F K KinF) (NetFA F K KinF)
          (NetFA_subset F K KinF) with âŸ¨x, xinK, clpointâŸ©
        use x
        exact And.intro xinK (clupoint_NetF_inclusion_implies_clpoint F K KinF x clpoint)

/- A set K of X is compact iff (K is empty or) any net s: D â†’ X contained in K has a subnet that converges to a point of K. -/
theorem compact_iff_net_has_convergent_subnet {X : Type*} [TopologicalSpace X] (K: Set X) : IsCompact K â†”
  K = âˆ… âˆ¨ âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’
  (âˆƒ (E: Type u_1) (h': DirectedSet E) (s': E â†’ X), âˆƒ x âˆˆ K, Subnet s s' âˆ§ Limit s' x) := by
    have : (âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’ âˆƒ (E: Type u_1) (h': DirectedSet E) (s': E â†’ X), âˆƒ x âˆˆ K, Subnet s s' âˆ§ Limit s' x) â†”
             (âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), (âˆ€ (d : D), s d âˆˆ K) â†’ âˆƒ x âˆˆ K, ClusterPoint s x) := by
              constructor
              Â· intro t D h s dinK
                rcases t D h s dinK with âŸ¨E, h', s', x, xinK, eqâŸ©
                use x, xinK
                rw [clpoint_iff_exists_subnet]
                use E, h', s'
              Â· intro t D h s dinK
                rcases t D h s dinK with âŸ¨x, xinK, eqâŸ©
                rw [clpoint_iff_exists_subnet] at eq
                rcases eq with âŸ¨E, h', s', eqâŸ©
                use E, h', s', x
    rw [compact_iff_net_has_accumulationpoint, this]

/- A function f: X â†’ Y is continuous at x iff for every net s: D â†’ X we have that the net f âˆ˜ s: D â†’ Y converges to f x. -/
theorem continuous_iff_image_of_net_converges {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] (f: X â†’ Y) (x : X):
  ContinuousAt f x â†” âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X), Limit s x â†’ Limit (f âˆ˜ s) (f x) := by
    constructor
    Â· intro fcontatx D h s limitsx
      unfold ContinuousAt at fcontatx
      rw [Filter.tendsto_def] at fcontatx
      rw [limit_net_iff_filter]
      intro V Vnhds
      simp only [FNet, Filter.mem_mk, Set.mem_setOf_eq]
      have := limitsx (f â»Â¹' V) (fcontatx V Vnhds)
      simp only [mem_preimage] at this
      simp only [comp_apply]
      assumption
    Â· intro cond
      unfold ContinuousAt
      rw [Filter.tendsto_def]
      intro V Vnhds
      have : Limit (NetF (ğ“ x)) x := by
        intro U Unhds
        use âŸ¨(x, U), And.intro (mem_of_mem_nhds Unhds) UnhdsâŸ©
        intro d xUled
        simp only [NetF]
        simp only [DirectedSetF_le_iff] at xUled
        exact xUled d.2.1
      rcases cond (DirectedSetF (ğ“ x)) (DirectedSetF.isntDirectedSet (ğ“ x)) (NetF (ğ“ x)) this V Vnhds with âŸ¨d, eqâŸ©
      have : d.1.2 âŠ† f â»Â¹' V := by
        intro z zind2
        rw [mem_preimage]
        have : d â‰¤ âŸ¨(z, d.1.2), And.intro zind2 d.2.2âŸ© := by
          rw [DirectedSetF_le_iff]
        have := eq âŸ¨(z, d.1.2), And.intro zind2 d.2.2âŸ© this
        simp only [NetF, comp_apply] at this
        assumption
      exact mem_of_superset d.2.2 this

/- A topological space X is T2 iff every net in X has at most one limit point. -/
theorem t2_iff_net_unique_limit {X : Type*} [TopologicalSpace X] :
  T2Space X â†” âˆ€ (D: Type u_1) (h: DirectedSet D) (s : D â†’ X) (x y : X), Limit s x â†’ Limit s y â†’ x = y := by
    rw [t2_iff_net_unique_limit_filter]
    constructor
    Â· intro cond
      intro D h s x y limitsx limitsy
      rw [limit_net_iff_filter] at *
      exact cond (FNet s) (FNet.instNeBot s) x y limitsx limitsy
    Â· intro cond F Fnebot x y limitFx limitFy
      rw [limit_filter_iff_net] at *
      exact cond (DirectedSetF F) (DirectedSetF.isntDirectedSet F) (NetF F) x y limitFx limitFy

/- A uniform space is complete iff is CompleteNet -/
theorem complete_iff_netcomplete {X: Type*} [UniformSpace X]:
  CompleteSpace X â†” CompleteNet X := by
    constructor
    Â· intro completeX
      unfold CompleteNet
      intro D h s cauchys
      rcases completeX.complete ((cauchy_net_iff_filter s).mp cauchys) with âŸ¨x, limitFxâŸ©
      use x
      rw [limit_net_iff_filter]
      assumption
    Â· intro completeX
      unfold CompleteNet at completeX
      apply completeSpace_of_isComplete_univ
      unfold IsComplete
      intro F cauchyF _
      rcases completeX (DirectedSetF F) (@DirectedSetF.isntDirectedSet X F cauchyF.1) (NetF F)
        ((@cauchy_filter_iff_net X _ F cauchyF.1).mp cauchyF) with âŸ¨x, limitsxâŸ©
      use x
      constructor
      Â· exact mem_univ x
      Â· rw [@limit_filter_iff_net X _ F cauchyF.1 x]
        assumption
